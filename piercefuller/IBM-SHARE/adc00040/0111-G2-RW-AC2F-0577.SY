       REM AUTO- AND CROSS-CORRELATION FUNCTION GENERATOR               RWAC2F  
 AC2F  SXD COMMON+001,4      SAVE IRC (FORM R (KDELTA))                 RWAC2F00
       STQ COMMON+002        K,0,0 (FIXED PT IN ADDRESS)                AC2F0001
       STO COMMON+003        N,0,0 (FIXED PT IN ADDRESS)                AC2F0002
       SBM COMMON+002        K                                          AC2F0003
       STO COMMON+004        N-K (FIXED PT IN ADDRESS)                  AC2F0004
       ADM AC2F+0027         233 (FLOAT N-K)                            AC2F0005
       FAD AC2F+0028         0 (NORMALIZE N-K)                          AC2F0006
       STO COMMON+005        N-K (FLOATING POINT)                       AC2F0007
       CAL 1,4               XDAT,0,YDAT                                AC2F0008
       ADM COMMON+004        N-K                                        AC2F0009
       STA AC2F+0017         XDAT+(N-K)                                 AC2F0010
       ARS 18                                                           AC2F0011
       ADM COMMON+003        N                                          AC2F0012
       STA AC2F+0018         YDAT+N                                     AC2F0013
       STZ COMMON+000        CLEAR CUM. PRODUCT (XI,YI+K)               AC2F0014
       LXA COMMON+004,4      (N-K) IN IRC                               AC2F0015
       TOV AC2F+0017         MAKE SURE AC LITE OFF                      AC2F0016
       LDQ 0,4               XDAT+(N-K)=(XI)                            AC2F0017
       FMP 0,4               YDAT+(N)=(YI+K)                            AC2F0018
       FAD COMMON+000        CUM. PRODUCT (XI,YI+K)                     AC2F0019
       STO COMMON+000                                                   AC2F0020
       TIX AC2F+0017,4,1          (N-K) (-1) (1)                        AC2F0021
       LXD COMMON+001,4      RESTORE IR4                                AC2F0022
       TNO AC2F+0025         TR IF NO OV/UNDERFLOW                      AC2F0023
       TRA 2,4               TR ERROR RETURN,(XIYI OV/UN FLOW)          AC2F0024
       FDP COMMON+005        (N-K),FLOATING POINT                       AC2F0025
       TRA 3,4               NORMAL RETURN,RESULT IN MQ                 AC2F0026
       OCT 233000000000      CONSTANT TO FLOAT FIXED PT.                AC2F0027
       DEC 0                 0 (TO NORMALIZE FL. PT. NO.)               AC2F0028
