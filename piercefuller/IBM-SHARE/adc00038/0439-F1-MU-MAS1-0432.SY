       REM MURA MATRIX ADD OR SUBTRACT, FIXED POINT                       MUMAS1
 MAS1  SXD MAS1+28,1       SAVE IR1                                     MAS10001
       SXD MAS1+29,2       SAVE IR2                                     MAS10002
       LXA MAS1+10,1       CLEAR IR1                                    MAS10003
       TOV MAS1+4          TURN OFF OVERFLOW LIGHT                      MAS10004
       CLA 1,4             ALPHA+1 WORD                                 MAS10005
       STA MAS1+26         PLANT B CALL ADDRESS, ADD                    MAS10006
       STA MAS1+27         PLANT B CALL ADDRESS, SUBTRACT               MAS10007
       ARS 18              POSITION DECREMENT                           MAS10008
       STA MAS1+15         PLANT A CALL ADDRESS                         MAS10009
       CLA 2,4             ALPHA+2 WORD                                 MAS10010
       PDX 0,2             SET IR2 TO R                                 MAS10011
       STA MAS1+18         PLANT C STORE ADDRESS                        MAS10012
       TMI MAS1+24         TEST FOR OPERATOR                            MAS10013
       CAL MAS1+26         IF PLUS, ADD                                 MAS10014
       SLW MAS1+16         PLANT OPERATOR                               MAS10015
       CLA **,1            A ELEMENT                                    MAS10016
       HTR 0               + OR - B ELEMENT                             MAS10017
       TOV 3,4             TEST FOR SUM TOO LARGE                       MAS10018
       STO **,1            C ELEMENT, RESULT                            MAS10019
       TXI MAS1+20,1,-1    DECREASE IR1 BY 1                            MAS10020
       TIX MAS1+15,2,1     LOOP R TIMES                                 MAS10021
       LXD MAS1+28,1       RESTORE IR1                                  MAS10022
       LXD MAS1+29,2       RESTORE IR2                                  MAS10023
       TRA 4,4             RETURN                                       MAS10024
       CAL MAS1+27         IF MINUS, SUBTRACT                           MAS10025
       TRA MAS1+14         X                                            MAS10026
       ADD **,1            ADDITION OPERATOR                            MAS10027
       SUB **,1            SUBTRACTION OPERATOR                         MAS10028
       HTR 0               IR1 REPOSITORY                               MAS10029
       HTR 0               IR2 REPOSITORY                               MAS10030
