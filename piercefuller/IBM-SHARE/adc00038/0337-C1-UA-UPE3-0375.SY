       REM   UNIVARIATE POLYNOMIAL EVALUATION FOR FORTRAN I PROGRAMS  UAUPE30000
       FUL                                                              UPE30001
           -1,,32               TO PUNCH CONTROL CARD  -1               UPE30002
       FUL                                                              UPE30003
           UPE+48,,32          TO PUNCH UPE3 CONTROL CARD               UPE30004
       REL                                                              UPE30005
       ORG 0                                                            UPE30006
 UPE   STQ UPE+45     UPE       SAVE X (ARGUEMENT)                      UPE30007
       SXD UPE+47,2    +1       SAVE INDEX 2                            UPE30008
       ARS 18          +2                                               UPE30009
       ADD UPE+47      +3       ADDRESS = UPE+46, ACC = CURVE NO (T)    UPE30010
       STA *+1         +4       UPE+46 + T = LOC IN TABLE OF CURVE LOCS.UPE30011
       CLA **          +5       LOCATION OF CURVE                       UPE30012
       STO UPE+46      +6                                               UPE30013
       COM             +7       ADDRESS = 6                             UPE30014
       PAX 5,2         +8       TWOS COMPLEMENT OF L(N) IN INDEX 2      UPE30015
       CLA -1,2        +9       X MINIMUM                               UPE30016
       LDQ UPE+45     +10       X (ARGUEMENT)                           UPE30017
       TLQ UPE+27     +11       X OUT OF RANGE (LESS THAN MINIMUM)      UPE30018
       CLA 1,2        +12       MAXIMUM FOR SECTION                     UPE30019
       SUB UPE+45     +13       X (ARGUEMENT)                           UPE30020
       TPL UPE+28     +14       TRA IF X IS IN THIS SECTION             UPE30021
       CLA 0,2        +15       N (DEGREE FOR SECTION)                  UPE30022
       TZE UPE+25     +16       X OUT OF RANGE (GREATER THAN MAXIMUM)   UPE30023
       LDQ UPE+46     +17       SAVE PREVIOUS L(N)-1                    UPE30024
       SXD UPE+24,2   +18       FOR EXTRAPOLATION IF X TOO LARGE        UPE30025
       ADD UPE+8      +19       5                                       UPE30026
       ADD UPE+46     +20       PREVIOUS L(N)-1                         UPE30027
       STA UPE+46     +21       NEW L(N)-1  (NEXT SECTION)              UPE30028
       COM            +22                                               UPE30029
       PAX 0,2        +23       TWOS COMPLEMENT OF L(N) NEXT SECTION    UPE30030
       TXI UPE+12     +24       TO TEST NEXT SECTION                    UPE30031
       STQ UPE+46     +25       X GREATER THAN MAXIMUM                  UPE30032
       LXD UPE+24,2   +26       RESTORE X2 FOR PREVIOUS SECTION         UPE30033
       HPR 511        +27       HPR 777(8) = X OUT OF RANGE             UPE30034
       LDQ 2,2        +28       ALPHA                                   UPE30035
       FMP UPE+45     +29       X                                       UPE30036
       FAD 3,2        +30       BETA                                    UPE30037
       STO UPE+45     +31       NORMALIZED X                            UPE30038
       LDQ 4,2        +32       C SUB N                                 UPE30039
       CLA 0,2        +33       N                                       UPE30040
       PAX 0,2        +34       DEGREE IN INDEX 2                       UPE30041
       ADD UPE+46     +35       L(N)-1                                  UPE30042
       ADD UPE+7      +36       6                                       UPE30043
       STA UPE+39     +37       L(C SUB 0) +1                           UPE30044
       FMP UPE+45     +38       C SUB N  *  NORMALIZED X                UPE30045
       FAD **,2       +39       C SUB (N-1), ... , C SUB 0              UPE30046
       STO UPE+46     +40                                               UPE30047
       LDQ UPE+46     +41                                               UPE30048
       TIX UPE+38,2,1 +42       PICK UP NEXT COEFFICIENT                UPE30049
       LXD UPE+47,2   +43       RESTORE INDEX 2                         UPE30050
       TRA 1,4        +44       EXIT                                    UPE30051
       BSS 1          +45       COMMON                                  UPE30052
       BSS 1          +46       COMMON+1                                UPE30053
           UPE+47     +47       X2 STORAGE AND CURVE TABLE LOCATOR      UPE30054
       END                      TO TERMINATE ASSEMBLY                   UPE30055
