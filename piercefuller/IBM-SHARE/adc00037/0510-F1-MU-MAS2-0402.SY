       REM MURA MATRIX ADD  OR SUBTRACT, FLOATING POINT                   MUMAS2
 MAS2  SXD MAS2+26,1       SAVE IR1                                     MAS20001
       SXD MAS2+27,2       SAVE IR2                                     MAS20002
       LXA MAS2+9,1        CLEAR IR1                                    MAS20003
       CLA 1,4             ALPHA+1 WORD                                 MAS20004
       STA MAS2+24         PLANT B CALL ADDRESS, +                      MAS20005
       STA MAS2+25         PLANT B CALL ADDRESS, -                      MAS20006
       ARS 18              TO ADDRESS PORTION                           MAS20007
       STA MAS2+14         PLANT A CALL ADDRESS                         MAS20008
       CLA 2,4             ALPHA+2 WORD                                 MAS20009
       PDX 0,2             SET IR2 TO R                                 MAS20010
       STA MAS2+16         PLANT C STORE ADDRESS                        MAS20011
       TMI MAS2+22         TEST FOR OPERATOR                            MAS20012
       CAL MAS2+24         IF PLUS, ADD                                 MAS20013
       SLW MAS2+15         PLANT OPERATOR WORD                          MAS20014
       CLA **,1            A ELEMENT                                    MAS20015
       HTR 0               + OR - B ELEMENT                             MAS20016
       STO **,1            C ELEMENT, RESULT                            MAS20017
       TXI MAS2+18,1,-1    DECREASE IR1                                 MAS20018
       TIX MAS2+14,2,1     LOOP R TIMES                                 MAS20019
       LXD MAS2+26,1       RESTORE IR1                                  MAS20020
       LXD MAS2+27,2       RESTORE IR2                                  MAS20021
       TRA 3,4             EXIT                                         MAS20022
       CAL MAS2+25         IF MINUS, SUBTRACT                           MAS20023
       TRA MAS2+13         X                                            MAS20024
       FAD **,1            ADDITION OPERATOR                            MAS20025
       FSB **,1            SUBTRACTION OPERATOR                         MAS20026
       HTR 0               IR1 REPOSITORY                               MAS20027
       HTR 0               IR2 REPOSITORY                               MAS20028
