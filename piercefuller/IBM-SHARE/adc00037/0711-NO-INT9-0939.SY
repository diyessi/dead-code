* NOINT9   198  08050                                                   NOINT901
INTERP BEGIN   6,7,0                                                    NOINT902
       AXT     13,1                                                     NOINT903
       STZ     COMMON+13,1                                              NOINT904
       TIX     *-1,1,1                                                  NOINT905
       CAL     1,4                                                      NOINT906
       STA     TIN                LOCATION OF TABLE A                   NOINT907
       STD     OXYGEN     J AT B = 17                                   NOINT908
       PDC    0,1      - J INTO INDEX REGISTER 1                        NOINT909
       SXD     NICKEL,1                                                 NOINT910
       SXD     ZIRCON,1                                                 NOINT911
       SXD     PEARL,1                                                  NOINT912
       PDX    0,1                                                       NOINT913
       SXA     GYPSUM,1      J AT B = 35                                NOINT914
       CAL     2,4                                                      NOINT915
       STA     ERBIUM      LOCATION OF TABLE B                          NOINT916
       ADD     ONE                                                      NOINT917
       STA     CURIUM        LOCATION OF DELTA  T                       NOINT918
       ADD     ONE                                                      NOINT919
       STA     BARIUM       LOCATION  OF  T  LAST                       NOINT920
       PDX    0,1      K  INTO INDEX REGISTER 1                         NOINT921
       SXA     SULFUR,1      K   AT B = 35                              NOINT922
       CAL     3,4                                                      NOINT923
       STA     OSMIUM    LOCATION OF TABLE  C                           NOINT924
       PDX    0,1          L INTO INDEX REGISTER 1                      NOINT925
       TXI     *+1,1,1      L + 1                                       NOINT926
       SXA     HELIUM,1     L+1   AT   B = 35                           NOINT927
       CAL     4,4                                                      NOINT928
       STA     SODIUM    M   IN ADDRESS PART                            NOINT929
       STD     IRON       N IN DECREMENT PART                           NOINT930
       PDX    0,3       N INTO INDEX REGISTERS  1 AND 2                 NOINT931
       TXI     *+1,2,1     N + 1                                        NOINT932
       SXA     GOLD,2      N + 1   IN ADDRESS PART                      NOINT933
       TXI     *+1,2,COMMON+13                                          NOINT934
       SXA     SILVER,2     COMMON + 13 + N + 1                         NOINT935
IRON   TXI     *+1,2,(N)    COMMON + 13 + 2N + 1                        NOINT936
       SXA     COPPER,2                                                 NOINT937
       SXA     CESIUM,2                                                 NOINT938
       TXI     *+1,2,1    COMMON + 13 + 2N + 2                          NOINT939
       SXA     RADIUM,2                                                 NOINT940
       TXI     *+1,1,COMMON+13                                          NOINT941
       SXA     ZINC,1    COMMON + 13 + N                                NOINT942
       LDQ     GOLD    N + 1  IN ADDRESS PART                           NOINT943
       VLM     OXYGEN,0,18    J(N+1)                                    NOINT944
       STA     ARGON          AT B = 35                                 NOINT945
       LXD IRON,1  THIS PROCEDURE TO GET RID OF MINUS                   NOINT946
       PXA 0,1     SIGNS AND PREFIXES, WHICH CAUSE TROUBLE              NOINT947
       LRS 35+1     (N/2INTEGER) IN ADDRESS PART OF MQ                  NOINT948
       VLM     OXYGEN,0,18                                              NOINT949
       STA     XENON      J(N/2 INTEGER)                                NOINT950
       ADM     1,4    ADD LOCATION OF TABLE A                           NOINT951
       STA     INDIUM      LOCATION OF TABLE  A + J(N/2INT)             NOINT952
TIN    AXC     0,1     - LOC TABLE A INTO X R                           NOINT953
       SXD     RUBY,1                                                   NOINT954
       CLS     ZERO                                                     NOINT955
       CAS     0,1    FIND LOCATION WHERE T = -0                        NOINT956
       XEC     NICKEL                                                   NOINT957
       TRA     CERIUM   LOCATION IS FOUND                               NOINT958
NICKEL TXI     *+1,1,('J)    EFFECTIVE ADDRESS UP BY  J                 NOINT959
       TXL     *-4,1,-30    ARBITRARY FIGURE TO STOP SEARCH             NOINT960
ONYX   RETURN  INTERP,2     EXCURSION THROUGH MEMORY                    NOINT961
*                           AT LEAST ONCE BEFORE STOP                   NOINT962
CERIUM SXA     *+1,1                                                    NOINT963
       AXC     0,2                                                      NOINT964
       PXA     0,2    TRUE FIGURE OF LOCATION OF  - 0                   NOINT965
       SBM     ARGON   LOCATION (- 0) - J(N+1)                          NOINT966
       STA     NEON                                                     NOINT967
       PAC     0,1            ADDITIONAL CARD,   2 OF 2           2 OF 2NOINT967
RUBY   TXL     TOPAZ,1,0    - LOC TABLE  A  IN DECREMENT                NOINT968
*              ERROR IF LESS THAN N + 1 POINTS                          NOINT969
       AXT     1,4                                                      NOINT970
       TRA     ONYX+1                                                   NOINT971
TOPAZ  ADM     XENON    LOC (-0) - J(N+1) + J(N/2INT)                   NOINT972
       STA     BERYL                                                    NOINT973
       AXT     0,2                                                      NOINT974
ERBIUM CLA     0    FIRST  T   THEN SUCCESSIVE T,S  INTO A C            NOINT975
OSMIUM STO     0,2    STORE  T  IN TABLE  C                             NOINT976
       TXI     *+1,2,-1     THIS TO LOCATE FUNCTIONS IN  C              NOINT977
       STO     RADON     CURRENT VALUE OF  T  AVAILABLE                 NOINT978
BERYL  FSB     0                                                        NOINT979
       TMI     INDIUM    TRANSFER IF FIRST N+1 POINTS OR                NOINT980
*                        IF WE CAN BRACKET T WITH N+1 VALUES            NOINT981
       CLA     NEON   PICK UP ADDRESS SO TO USE LAST N+1                NOINT982
       TRA     GALENA                                                   NOINT983
INDIUM CLA     0                                                        NOINT984
       FSB     RADON                                                    NOINT985
       TMI     OPAL  TRANSFER IF WE CAN BRACKET  T  PROPERLY            NOINT986
       CAL     TIN  LOCATION TABLE  A IN ADDR PART                      NOINT987
       TRA     GALENA   USE FIRST  N+1 POINTS                           NOINT988
OPAL   LAC     TIN,1                                                    NOINT989
       CLA     RADON  CURRENT   T                                       NOINT990
       CAS     0,1  LOCATE THE N+1 POINTS WHICH BRACKET                 NOINT991
ZIRCON TXI     *-1,1,('J)                                               NOINT992
       NOP                                                              NOINT993
       PXA     0,1                                                      NOINT994
       ADM     GYPSUM                                                   NOINT995
       ADM     XENON                                                    NOINT996
       PAX     0,4  -(LOC(A SUB I) - J - J(N/2INT))                     NOINT997
       TRA     GOLD                                                     NOINT998
*WE HAVE COMPUTED THE FIRST T ADDRESS OF BLOCK OF N+1 POINTS            NOINT999
GALENA PAC     0,4                                                      NOINT9??
GOLD   AXT     0,1   N+1 IN ADDRESS                                     NOINT9?A
       SXA     GARNET,4                                                 NOINT9?B
QUARTZ LDQ     0,4   FIRST  T OF SET                                    NOINT9?C
*GET N+1 VALUES OF INDEPENDENT VARIABLE AND STORE IN BUFFER             NOINT9?D
RADIUM STQ     0,1   COMMON + 13 + 2N + 2                               NOINT9?E
PEARL  TXI     *+1,4,('J)                                               NOINT9?F
       TIX     QUARTZ,1,1                                               NOINT9?G
GARNET AXC     0,1                                                      NOINT9?H
       PXA     0,1   TRUE FIGURE ADDRESS OF FIRST  T                    NOINT9?I
       SXA     IODINE,1    LOCATION OF T SUB 0                          NOINT9A?
       ADM     HELIUM   ADD L + 1                                       NOINT9AA
       STA     BORON                                                    NOINT9AB
       ADM     SODIUM    ADD  M                                         NOINT9AC
       STA     PANSY                                                    NOINT9AD
TRONA  CAL     IODINE                                                   NOINT9AE
       ADD     ONE                                                      NOINT9AF
       STA     IODINE                                                   NOINT9AG
       LAS     BORON                                                    NOINT9AH
       TRA     *+2                                                      NOINT9AI
       TRA     SODIUM  IF LOC(T 0)+ I = LOC(T 0)+ L + 1                 NOINT9B?
       LXA     GOLD,4                                                   NOINT9BA
TALC   STA     *+1                                                      NOINT9BB
       LDQ     0   LOC(T 0)+ I                                          NOINT9BC
*              GET N+1 VALUES OF  Y                                     NOINT9BD
SILVER STQ     0,4   COMMON + 13 + N + 1  IN ADDRESS                    NOINT9BE
       ADM     GYPSUM    ADD  J                                         NOINT9BF
       TIX     TALC,4,1                                                 NOINT9BG
LEAD   CLA     VIOLET    SET Y(X) = Y(1)                                NOINT9BH
       STO*    OSMIUM                                                   NOINT9BI
       CLA     FLT1                                                     NOINT9C?
       STO     CARBON  PRESET  A SUB I = 1                              NOINT9CA
       LXD     IRON,1    N                                              NOINT9CB
       TXL     BORAX,1,0   IF POINT INTERPOLATION                       NOINT9CC
BARITE SXA     AGATE,1                                                  NOINT9CD
       LXD     IRON,4                                                   NOINT9CE
       CLA     RADON   CURRENT  T   VALUE                               NOINT9CF
COPPER FSB     0,1    COMMON + 13 + 2N + 1                              NOINT9CG
       STO     IRIS   COMPUTE T - T SUB I                               NOINT9CH
RUTILE CLA*    RADIUM   COMPUTE DIVIDED DIFFERENCES                     NOINT9CI
CESIUM FSB     0,4    COMMON + 13 + 2N+ 1                               NOINT9D?
       STO     SPINEL                                                   NOINT9DA
       CLA*    SILVER                                                   NOINT9DB
ZINC   FSB     0,4   COMMON + 13 + N                                    NOINT9DC
       FDP     SPINEL                                                   NOINT9DD
       STQ*    ZINC                                                     NOINT9DE
       TXI     *+1,4,-1                                                 NOINT9DF
       TIX     RUTILE,1,1                                               NOINT9DG
       LDQ     IRIS                                                     NOINT9DH
       FMP     CARBON                                                   NOINT9DI
       FRN                                                              NOINT9E?
       STO     CARBON                                                   NOINT9EA
       XCA                                                              NOINT9EB
       FMP     VIOLET                                                   NOINT9EC
       FRN                                                              NOINT9ED
       FAD*    OSMIUM                                                   NOINT9EE
       STO*    OSMIUM                                                   NOINT9EF
AGATE  AXT     0,1                                                      NOINT9EG
       TIX     BARITE,1,1  INCREASE ORDER OF DIVIDED DIFFERENCES        NOINT9EH
BORAX  TXI     TRONA,2,-1   RETURN FOR NEXT FUNCTION                    NOINT9EI
SODIUM AXT     0,1     M                                                NOINT9F?
       TXL     ROSE,1,0   IF NONE TO MOVE                               NOINT9FA
PANSY  CLA     0,1   LOC(T 0)+ L+1 + M                                  NOINT9FB
       STO*    OSMIUM                                                   NOINT9FC
       TXI     *+1,2,-1                                                 NOINT9FD
       TIX     PANSY,1,1                                                NOINT9FE
ROSE   CLA     SULFUR   ADD  K    IF K=0, EVEN INTERVALS                NOINT9FF
       TZE     LILY                                                     NOINT9FG
       ADM     ERBIUM  LOCATION OF PREVIOUS  T                          NOINT9FH
       STA     ERBIUM                                                   NOINT9FI
       CLA*    ERBIUM                                                   NOINT9G?
       TNZ     OSMIUM                                                   NOINT9GA
       TPL     OSMIUM    -0 IS END OF THE TABLE                         NOINT9GB
FERN   RETURN  INTERP                                                   NOINT9GC
LILY   CLA     RADON                                                    NOINT9GD
BARIUM CAS     0      LOCATION OF T LAST                                NOINT9GE
       RETURN  INTERP                                                   NOINT9GF
       RETURN  INTERP                                                   NOINT9GG
CURIUM FAD     0   ADD DELTA  T                                         NOINT9GH
       TRA     OSMIUM                                                   NOINT9GI
ONE    DEC     1                                                        NOINT9H?
FLT1   DEC     1.                                                       NOINT9HA
ZERO   DEC     0                                                        NOINT9HB
OXYGEN SYN     COMMON+0   J IN DECREMENT PART                           NOINT9HC
SULFUR SYN     COMMON+1  K IN ADDRESS PART                              NOINT9HD
HELIUM SYN     COMMON+2   L+1 IN ADDRESS                                NOINT9HE
ARGON  SYN     COMMON+3   J(N+1)  B = 35                                NOINT9HF
XENON  SYN     COMMON+4   J(N/2INTEGER)                                 NOINT9HG
NEON   SYN     COMMON+5 LOC (-0) - J(N+1)                               NOINT9HH
RADON  SYN     COMMON+6    CURRENT  T                                   NOINT9HI
IODINE SYN     COMMON+7   LOC OF(T 0)IN ADDR PART                       NOINT9I?
BORON  SYN     COMMON+8  LOC OF T0 + L + 1                              NOINT9IA
CARBON SYN     COMMON+9                                                 NOINT9IB
SPINEL SYN     COMMON+10   TEMPORARY IN COMPUTING DIV DIFF              NOINT9IC
IRIS   SYN     COMMON+11                                                NOINT9ID
GYPSUM SYN     COMMON+12                                                NOINT9IE
VIOLET SYN     COMMON+13                                                NOINT9IF
('J)   EQU     0                                                        NOINT9IG
(N)    EQU     0                                                        NOINT9IH
